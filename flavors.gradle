ext.flavorConfig = {

    flavorDimensions "version"
    productFlavors {
        dev {
            dimension "version"
            ext.myApplicationIdSuffix = '.dev'
            ext.versionNameSuffix = '-dev'
            manifestPlaceholders = [
                    iconApp: "@drawable/ic_popcorn_dev",
                    launchActivityName: "com.studio.sevenapp.android.popkornstudio.splash.SplashDevActivity"
            ]
        }
        prod {
            dimension "version"
            manifestPlaceholders = [
                    iconApp: "@drawable/ic_popcorn",
                    launchActivityName: "com.studio.sevenapp.android.popkornstudio.splash.SplashActivity"
            ]
        }
    }

    productFlavors.all { flavor ->
        if (flavor.hasProperty('myApplicationIdSuffix') && isApplicationProject()) {
            flavor.applicationIdSuffix = flavor.myApplicationIdSuffix
        }
    }

}

def isApplicationProject() {
    return project.android.class.simpleName.startsWith('BaseAppModuleExtension')
    // in AGP 3.1.x with library modules instead of feature modules:
    // return project.android instanceof com.android.build.gradle.AppExtension
}